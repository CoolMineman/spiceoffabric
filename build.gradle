plugins {
	id 'fabric-loom' version '0.2.3-SNAPSHOT'
	id 'maven-publish'
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '4.0.0'
}

evaluationDependsOnChildren()

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
	mavenLocal()
    maven { url "https://minecraft.curseforge.com/api/maven"}
	maven { url 'https://jitpack.io' }
	mavenCentral()
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	// exp4j
	shadow 'net.objecthunter:exp4j:0.4.8'
	// Tweed
	include "${project.tweed_maven}:${project.tweed_version}"
	modCompile "${project.tweed_maven}:${project.tweed_version}"
	// Fabric
	modCompile "net.fabricmc.fabric-api:fabric-networking-v0:+"
	include "net.fabricmc.fabric-api:fabric-networking-v0:+"
	modCompile "net.fabricmc.fabric-api:fabric-api-base:+"
	include "net.fabricmc.fabric-api:fabric-api-base:+"
	// Cloth Config
	include "cloth-config:ClothConfig:${project.clothconfig_version}"
	modCompile "cloth-config:ClothConfig:${project.clothconfig_version}"
	// ModMenu
	modCompile "io.github.prospector.modmenu:ModMenu:${project.modmenu_version}"
	// AppleSkin
	modCompile 'com.github.squeek502:AppleSkin:1.14-fabric-SNAPSHOT'
	//modCompile 'squeek.appleskin:AppleSkin:unspecified'
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
	classifier = "plain"
}

shadowJar {
	configurations = [project.configurations.shadow]
	classifier = ""
	relocate "net.objecthunter", "de.siphalor.spiceoffabric.shadow.net.objecthunter"
}

remapJar {
	dependsOn shadowJar
	file shadowJar.archivePath
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(shadowJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
